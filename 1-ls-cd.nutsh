lesson_name("Umsehen und Bewegen - ls und cd")
go_home

run("touch linker_schuh")
run("mkdir schrank")
run("touch schrank/jacke")
run("touch schrank/hut")
run("rm -rf schrank/magische_tür")

"Als erstes lernen Sie die zwei wichtigsten Befehle: Wie man sich im Dateisystem umsieht und herumbewegt."

"Tippen Sie mal `ls` ein und drücken Sie Enter."

common_mistakes, stayinroot {
    prompt {
        if command == "ls" {
            expect("ls")
            "Genau. `ls` steht kurz für \"list\" und zeigt Ihnen die Dateien
            und Verzeichnisse an, die sich in Ihrem aktuellen Verzeichnis
            befinden. Die Verzeichnisse werden dabei blau dargestellt, die
            weißen Einträge sind Dateien."

            say("Sie sind gerade in einem Ordner namens `"+run("pwd")+"` - der aktuelle
            Ordner steht auch immer im Prompt, der mit dem Dollarzeichen
            endet.")

            break
        }
    }

    "Sie haben vielleicht gesehen, dass sich hier ein Verzeichnis namens
    `schrank` befindet. Um dieses zu Ihrem aktuellen Verzeichnis zu machen,
    tippen Sie `cd` (vom Englischen \"change directory\"), dann ein Leerzeichen
    und dann den Namen des Verzeichnisses, in das Sie möchten. Bewegen Sie sich
    damit doch mal in den Schrank und sehen Sie sich mit `ls` darin um!"

    prompt {
        if run("pwd") == run("echo $ROOT/schrank") {
            if output =~ "jacke" {
                expect("cd schrank; pwd; ls")
                "Genau. Haben Sie bemerkt, wie sich der Prompt geändert hat?"
                break
            }
        }
    }

    "Und wenn Sie wieder aus dem Schrank herausmöchten? Die Abkürzung für das
    Verzeichnis oberhalb des aktuellen sind zwei Punkte: `..`!"

    prompt {
        if run("pwd") == run("echo $ROOT") {
            expect("cd ..")
            break
        }
    }

    "Gut. So, nun brauchen wir ein wenig Magie... *pling*"

    "[Der Schrank rumpelt und ächzt]"

    run("mkdir -p $ROOT/schrank/magische_tür/tür{1..3}")
    run("touch $ROOT/schrank/magische_tür/tür2/rechter_schuh")

    "Im Schrank hat sich nun etwas verändert. Gehen Sie hinein und suchen Sie den rechten Schuh."

    prompt {
        if output =~ "rechter_schuh" {
            if run("pwd") == run("echo $ROOT/schrank/magische_tür/tür2") {
                expect("cd schrank/magische_tür/tür2; ls")
                "Sie haben ihn gefunden! Nun kommen Sie bitte wieder aus dem Schrank heraus!"
                break
            }
        }
        if command =~ "ls" {
            if output == "" {
                expect("ls schrank/magische_tür/tür1")
                if !(run("echo $NUTSH_ONCE") == "ok") {
                    "Dieses Verzeichnis scheint leer zu sein."
                    run("NUTSH_ONCE=ok")
                }
            }
        }
    }

    prompt {
        if run("pwd") == run("echo $ROOT") {
            expect("cd ../../..")
            expect("cd ..; cd ..; cd ..")
            break
        }
    }

    "Gut! Das war eine erste Einführung in `ls` und `cd`, wir werden uns diesen Befehlen später noch genauer widmen."
}
