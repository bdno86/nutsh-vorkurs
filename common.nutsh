def exit_code {
    return(run("echo $?"))
}

def success {
    return(exit_code == "0")
}

def go_home {
    run(`ROOT="$HOME"/.nutsh`)
    run(`mkdir -p "$ROOT"`)
    run(`cd "$ROOT"`)
}

def make_home {
    go_home
    run(`cd "$ROOT"`)
    run(`mkdir -p schrank wohnzimmer schlafzimmer badezimmer küche küche/esszimmer`)
    run(`touch wohnzimmer/tisch schlafzimmer/bett`)
}

def common_mistakes {
    if command == "1s" {
        "Das ist ein kleines L, keine Eins! Probieren Sie's nochmal!"
    }
    if command == ".." {
        "`..` ist der Name des Verzeichnisses, Sie müssen noch dazusagen, was
        Sie damit machen möchten. Um \"hinzugehen\", schreiben Sie `cd` davor."
    }
    if command == "cd.." {
        "Da fehlt noch ein Leerzeichen zwischen `cd` und `..`!"
    }
}

def stayinroot {
    if !(run("pwd") =~ run("echo $ROOT")) {
        run("cd $ROOT")
        "Bleiben Sie bitte erstmal in diesem Ordner."
    }
}

def runs(cmd) {
    run(cmd)
    return(exit_code == "0")
}

def test(condition) {
    return(runs("[[ "+condition+" ]]"))
}

def dir(d) {
    return(test("-d \""+d+"\""))
}

def file(f) {
    return(test("-f \""+f+"\""))
}

def help {
    return(command =~ `echo\s+(help|hilfe)`)
}

def ready {
    return(command =~ `echo\s+(fertig|ready)`)
}

def in(d) {
    return(run("pwd") == d)
}
